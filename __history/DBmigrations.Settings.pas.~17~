unit DBmigrations.Settings;

interface

uses
  System.SysUtils, System.JSON, System.IOUtils, System.NetEncoding,
  DBmigrations.Interfaces;

type
  TMACConfig = class(TInterfacedObject, IConfigManager)
  private
    FFilePath: string;
    FJSON: TJSONObject;

  const
    FileName = 'settings.json';

  const
    EncryptionKey = 'MinhaChaveSegura'; // Deve ser armazenada com seguran�a

    function Encrypt(const Value: string): string;
    function Decrypt(const Value: string): string;
    procedure LoadFileSettings;
    procedure SaveSettings;
  public
    constructor Create;
    destructor Destroy; override;
    class function New: IConfigManager; overload;
    procedure SetConfiguracao(AChave, Valor: string);
    function GetConfiguracao(AChave: string): string;

  end;

function MACConfig: IConfigManager;

implementation

uses
  System.Classes;

{ TMACConfig }

function MACConfig: IConfigManager;
begin
  Result := TMACConfig.Create;
end;

constructor TMACConfig.Create;
begin
  FFilePath := TPath.Combine(ExtractFilePath(ParamStr(0)), FileName);
  FJSON := TJSONObject.Create;
  LoadFileSettings;
end;

destructor TMACConfig.Destroy;
begin
  SaveSettings;
  FJSON.Free;
  inherited;
end;

function TMACConfig.Encrypt(const Value: string): string;
begin
  Result := TNetEncoding.Base64.Encode(Value);
end;

function TMACConfig.Decrypt(const Value: string): string;
begin
  Result := TNetEncoding.Base64.Decode(Value);
end;

function TMACConfig.GetConfiguracao(AChave: string): string;
var
  Value: TJSONValue;
begin
  Value := FJSON.GetValue(AChave);
  if Assigned(Value) then
    Result := Decrypt(Value.Value)
  else
    Result := '';
end;

procedure TMACConfig.SetConfiguracao(AChave, Valor: string);
var
  FValue: string;
begin
  FJSON.TryGetValue<string>(AChave, FValue);

  if NOT FValue.IsEmpty then
    FJSON.RemovePair(AChave);

  FJSON.AddPair(AChave, Encrypt(Valor));
end;

procedure TMACConfig.LoadFileSettings;
var
  FileContent: string;
begin
  if not TFile.Exists(FFilePath) then
  begin
    FJSON.AddPair('DBMigrations', '1.0');
    SaveSettings;
    Exit;
  end;

  try
    FileContent := TFile.ReadAllText(FFilePath);

    FJSON.Free;
    FJSON := TJSONObject.ParseJSONValue(FileContent) as TJSONObject;
    if not Assigned(FJSON) then
      raise Exception.Create('Arquivo de configuração inválido.');
  except
    on E: Exception do
    begin
      FJSON := TJSONObject.Create;
      raise Exception.CreateFmt('Erro ao carregar configuração: %s',
        [E.Message]);
    end;
  end;
end;

class function TMACConfig.New: IConfigManager;
begin
  Result := Self.Create;
end;

procedure TMACConfig.SaveSettings;
var
  JSONString: string;
begin
  try
    JSONString := FJSON.ToJSON;

    TFile.WriteAllText(FFilePath, JSONString);
  except
    on E: Exception do
      raise Exception.CreateFmt('Erro ao salvar configura��es: %s',
        [E.Message]);
  end;
end;

end.
